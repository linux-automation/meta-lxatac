DISTRO = "tacos"
DISTRO_NAME = "TAC OS - The LXA TAC operating system"
DISTRO_VERSION = "24.09+dev"
DISTRO_CODENAME = "tacos-styhead"

MAINTAINER = "Linux Automation GmbH <info@linux-automation.com>"

LOCALCONF_VERSION = "2"

DISTRO_FEATURES = "\
    alsa \
    argp \
    bluetooth \
    ext2 \
    ipv6 \
    minidebuginfo \
    multiarch \
    nfs \
    polkit \
    rauc \
    seccomp \
    security \
    systemd \
    usbgadget \
    usbhost \
    usrmerge \
    virtualization \
    wifi \
    xattr \
    zeroconf \
"

# Select systemd as init manager
INIT_MANAGER ?= "systemd"

# Set static uids/gids to allow migration and shared partitions
# between update slots.
USERADDEXTENSION = "useradd-staticids"
USERADD_ERROR_DYNAMIC = "error"
USERADD_UID_TABLES = "files/passwd"
USERADD_GID_TABLES = "files/group"

# add build info file (/etc/build) to rootfs to ease traceability
INHERIT += "image-buildinfo"

# prepare to use icecc
# to enable icecc in your build, add ICECC_DISABLED = "" to your local.conf
INHERIT_DISTRO:append = " icecc"
ICECC_DISABLED ??= "1"

# add poky sanity bbclass
INHERIT += "poky-sanity"
ERROR_QA:remove = "patch-status buildpaths"
WARN_QA:append = "patch-status buildpaths"

require conf/distro/include/poky-world-exclude.inc
require conf/distro/include/no-static-libs.inc
require conf/distro/include/yocto-uninative.inc
require conf/distro/include/security_flags.inc
INHERIT += "uninative"

# Enable creation of SPDX manifests
INHERIT += "create-spdx"

# Create a directory for each package in `/usr/share/licenses`
# and place licensing information there.
LICENSE_CREATE_PACKAGE = "1"
COPY_LIC_MANIFEST = "1"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"

# recommend ipk package management - make sure to comment or set it in local.conf.sample
PACKAGE_CLASSES ?= "package_ipk"

LABGRID_USE_DEVEL_VERSION ?= "1"
RAUC_USE_DEVEL_VERSION = "1"
